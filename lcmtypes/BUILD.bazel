# -*- python -*-

package(default_visibility = ["//visibility:public"])

load("//tools/lint:lint.bzl", "add_lint_tests")
load(
    "@drake//tools/workspace/lcm:lcm.bzl",
    "lcm_cc_library",
    "lcm_java_library",
    "lcm_py_library",
)

LCM_SRCS = glob(["*.lcm"])

# Also provide the srcs in a package-specific subdir.
[
    genrule(
        name = "_cp_" + x,
        srcs = [x],
        outs = ["rs2_lcm/" + x],
        cmd = "cp $< $@",
        visibility = ["//visibility:private"],
    )
    for x in LCM_SRCS
]

filegroup(
    name = "lcm_srcs_with_subdir",
    srcs = [
        ":rs2_lcm/" + x
        for x in LCM_SRCS
    ],
)

lcm_cc_library(
    name = "lcmtypes_rs2_cc",
    lcm_package = "rs2_lcm",
    lcm_srcs = LCM_SRCS,
    tags = ["nolint"],
)

filegroup(
    name = "cxx_srcs",
    srcs = [
        ":rs2_lcm/" + x.replace(".lcm", ".hpp")
        for x in LCM_SRCS
    ],
)

lcm_py_library(
    name = "py_rs2_lcmtypes",
    lcm_package = "rs2_lcm",
    lcm_srcs = LCM_SRCS,
)

lcm_java_library(
    name = "lcmtypes_rs2_lcm",
    lcm_package = "rs2_lcm",
    lcm_srcs = LCM_SRCS,
)

filegroup(
    name = "java_srcs",
    srcs = [
        ":rs2_lcm/" + x.replace(".lcm", ".java")
        for x in LCM_SRCS
    ],
)


add_lint_tests()
